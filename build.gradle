apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion SysConfig.compileSdkVersion

    defaultConfig {
        minSdkVersion SysConfig.minSdkVersion
        targetSdkVersion SysConfig.targetSdkVersion
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"

        checkMVVMConfigProperty(IncludeLib.retrofit2RxJava2, "retrofit2RxJava2")
        manifestPlaceholders = [
                "isRetrofitUseRx": IncludeLib.retrofit2RxJava2
        ]
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            aaptOptions {
                cruncherEnabled false
            }
        }

        beta {
            initWith release
        }
    }

    buildFeatures{
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "${JavaVersion.VERSION_1_8}"
    }

    sourceSets {
        main {
            java {
                /*
                 TODO 这里似乎无法根据配置来排除某些类，而是取决于是否使用了某类来自动排除
                 */
            }
        }
    }
}

/**
 * TODO
 * 根据配置文件，决定是否引入某些库。
 * 如果框架中有用到某库，那么在排除该库的时候需要 compileOnly，以便编译通过，比如 room
 * kapt 可能需要单独在 module 中声明
 */
dependencies {
    api fileTree(dir: "libs", include: ["*.jar"])
    // 这两个是创建项目默认就有的库
    api Deps.kotlinStdLib
    api Deps.kotlinKTX
    // 兼容库
    api Deps.appcompat
    // 约束布局
    api Deps.constraintLayout
    // 侧滑布局，是用来界面侧滑退出的，因为只有一个类，就不做可配置去除了
    api Deps.slidingPaneLayout

    // 以下三个是为了使用最新的特性
    api Deps.activity
    api Deps.activityKTX
    api Deps.fragment

    // 在 Lifecycles 中使用协程，视图销毁则自动取消
    api Deps.lifecycleRuntimeKTX
    // 在 LiveData 中使用协程
    api Deps.lifecycleLiveDataKTX
    // 简化 Fragment 的 API 调用，同时通过 viewModels 扩展方法创建 vm 来的非常容易
    api Deps.fragmentKTX
    // 使用 Java8 替换 lifecycleCompiler，让 vm 可以感知 v 生命周期
    api Deps.lifecycleJava8

    // 监控应用的生命周期
    checkMVVMConfigProperty(IncludeLib.lifecycleProcess, "lifecycleProcess")
    if (IncludeLib.lifecycleProcess) {
        api Deps.lifecycleProcess
    } else {
        compileOnly Deps.lifecycleProcess
    }

    // 内嵌的加载中库
    checkMVVMConfigProperty(IncludeLib.loadSir, "loadSir")
    if (IncludeLib.loadSir) {
        api Deps.loadSir
    } else {
        compileOnly Deps.loadSir
    }

    // RecyclerView
    checkMVVMConfigProperty(IncludeLib.recyclerView, "recyclerView")
    if (IncludeLib.recyclerView) {
        api Deps.recyclerView
    } else {
        compileOnly Deps.recyclerView
    }

    // 数据库
    checkMVVMConfigProperty(IncludeLib.room, "room")
    if (IncludeLib.room) {
        api Deps.roomRuntime
        kapt Deps.roomCompiler
    } else {
        compileOnly Deps.roomRuntime
    }

    // Android 提供的下拉刷新，上拉加载可以使用第三方的 SmartRefreshLayout
    checkMVVMConfigProperty(IncludeLib.swipeRefreshLayout, "swipeRefreshLayout")
    if (IncludeLib.swipeRefreshLayout) {
        api Deps.swipeRefreshLayout
    } else {
        compileOnly Deps.swipeRefreshLayout
    }

    // 网络，默认使用 gson 解析 json
    checkMVVMConfigProperty(IncludeLib.retrofit2, "retrofit2")
    if (IncludeLib.retrofit2) {
        api Deps.retrofit2
        api Deps.gson
        api Deps.gsonConverter
    } else {
        compileOnly Deps.retrofit2
        compileOnly Deps.gsonConverter
    }

    // 图片，使用 glide
    checkMVVMConfigProperty(IncludeLib.glide, "glide")
    if (IncludeLib.glide) {
        api Deps.glide
        kapt Deps.glideCompiler
    } else {
        compileOnly Deps.glide
    }

    // 权限申请
    checkMVVMConfigProperty(IncludeLib.livePermissions, "livePermissions")
    if (IncludeLib.livePermissions) {
        api Deps.livePermissions
    } else {
        compileOnly Deps.livePermissions
    }

    // rx 系列，Kotlin 最好使用 flow，不要使用 rx
    checkMVVMConfigProperty(IncludeLib.roomRxJava2, "roomRxJava2")
    checkMVVMConfigProperty(IncludeLib.retrofit2RxJava2, "retrofit2RxJava2")
    checkMVVMConfigProperty(IncludeLib.rxJava2, "rxJava2")

    if (IncludeLib.roomRxJava2 || IncludeLib.retrofit2RxJava2 || IncludeLib.rxJava2) {
        // 上面三个任意一个开启了，都需要以下这两个
        api Deps.rxAndroid2
        api Deps.rxJava2

        if (IncludeLib.roomRxJava2) {
            api Deps.roomRxJava2
        }
        if (IncludeLib.retrofit2RxJava2) {
            api Deps.retrofit2RxJava2
        } else {
            // 避免和上述的 api 引用冲突，需要去除相关库
            compileOnly(Deps.retrofit2RxJava2) {
                exclude group: 'io.reactivex.rxjava2'
                exclude group: 'org.reactivestreams'
            }
        }
    } else {
        // 不使用 rx，以下是为了让封装的代码编译通过
        compileOnly(Deps.retrofit2RxJava2)
        compileOnly Deps.rxAndroid2
    }

    // debug 阶段内存泄露检测
    checkMVVMConfigProperty(IncludeLib.leakCanary2, "leakCanary2")
    if (IncludeLib.leakCanary2) {
        debugImplementation Deps.leakCanary2
    }
}